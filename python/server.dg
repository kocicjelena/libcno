import '/asyncio'

import '/cloader'
import '/cno'


respond = async $ loop ~>
  print   '--' 'request:' @method @path
  for (k, v) in @headers => print '  ' k v
  async for p in @payload => print '++' 'payload:' (len p) 'bytes'
  @push "GET" "/hello" [(':authority', 'localhost'), (':scheme', 'http')]
  await @respond 200 [('server', 'libcno/0.1'), ('content-length', '14')] b'Hello, World!\n'


loop = asyncio.get_event_loop!
serv = loop.run_until_complete $ loop.create_server (-> cno.Server loop respond) '' 8000
print 'listening on port 8000'
except err => loop.run_forever!
       err :: KeyboardInterrupt =>
       finally => serv.close!
